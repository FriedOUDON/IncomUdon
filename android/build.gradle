buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        //noinspection AndroidGradlePluginVersion
        classpath 'com.android.tools.build:gradle:8.10.1'
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: qtGradlePluginType

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    //noinspection GradleDependency
    implementation 'androidx.core:core:1.16.0'
}

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qtAndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     * - qtGradlePluginType - whether to build an app or a library
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    namespace = androidPackageName
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion
    ndkVersion = androidNdkVersion

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qtAndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qtAndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qtAndroidDir + '/res', 'res']
            resources.srcDirs = ['resources']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
        options.compilerArgs += ['-Xlint:all']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError = false
    }

    aaptOptions {
        // Do not compress Qt binary resources file
        noCompress 'rcc'
    }

    defaultConfig {
        resConfig "en"
        minSdkVersion qtMinSdkVersion
        targetSdkVersion qtTargetSdkVersion
        ndk.abiFilters = qtTargetAbiList.split(",")
    }
}

// Keep a stable copy under the androiddeployqt output directory.
tasks.register("copyApkToOutputDir") {
    doLast {
        def apkFiles = fileTree("$buildDir/outputs/apk/debug").matching {
            include "*.apk"
        }.files

        if (apkFiles == null || apkFiles.isEmpty()) {
            println("No APK found under $buildDir/outputs/apk/debug")
            return
        }

        def latestApk = apkFiles.max { it.lastModified() }
        copy {
            from latestApk
            into projectDir
            rename { "appIncomUdon.apk" }
        }

        def exportDir = new File(projectDir, "output")
        exportDir.mkdirs()
        copy {
            from latestApk
            into exportDir
            rename { "appIncomUdon-debug.apk" }
        }

        println("APK copied to: ${new File(projectDir, 'appIncomUdon.apk').absolutePath}")
        println("APK copied to: ${new File(exportDir, 'appIncomUdon-debug.apk').absolutePath}")
    }
}

tasks.matching { it.name == "assembleDebug" }.configureEach {
    finalizedBy("copyApkToOutputDir")
}
